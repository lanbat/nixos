# modules/core/settings.nix
#
# Deployment and host settings. Values are injected from deploy.nix by
# lib/mkHost.nix; none have defaults except optional features.
{ lib, ... }:

let
  inherit (lib) mkOption types;

  ipv4 = types.strMatching "([0-9]{1,3}\\.){3}[0-9]{1,3}";

  hostSubmodule = types.submodule {
    options = {
      role = mkOption {
        type = types.enum [
          "server"
          "storage-pi"
          "voice-pi"
        ];
        description = "Infrastructure role of this host.";
      };

      networking = {
        ip = mkOption {
          type = ipv4;
          description = "Static IPv4 address of this host.";
        };
        interface = mkOption {
          type = types.str;
          description = "Network interface that gets the static address.";
        };
        hostname = mkOption {
          type = types.str;
          description = "Hostname of this host.";
        };
      };

      overlay = mkOption {
        type = types.nullOr (
          types.submodule {
            options = {
              ip = mkOption {
                type = types.str;
                example = "10.100.0.1";
                description = "Address this host holds on the overlay.";
              };
              publicKey = mkOption {
                type = types.str;
                description = "Public key other hosts encrypt to. Not a secret.";
              };
              endpoint = mkOption {
                type = types.nullOr types.str;
                default = null;
                example = "vpn.example.com:51820";
                description = ''
                  Where this host can be dialled, when it can be. A host without
                  one keeps a path open toward those that have one, so a
                  rendezvous host is just a host with an endpoint rather than a
                  special case.
                '';
              };
            };
          }
        );
        default = null;
        description = "This host's place on the overlay, or null if it does not join one.";
      };

      services = mkOption {
        type = types.listOf types.str;
        default = [ ];
        example = [
          "caddy"
          "frigate"
        ];
        description = ''
          Services this host runs, by name, resolved through the service
          registry in modules/core/service-registry.nix. A host with an empty
          list runs no lanbat service.
        '';
      };

      disks = mkOption {
        type = types.attrsOf types.str;
        default = { };
        description = "Role-specific disks. Server role uses disks.system.";
      };

      storage = mkOption {
        type = types.submodule {
          options = {
            drives = mkOption {
              type = types.attrsOf types.str;
              default = { };
              description = "Storage drive by-id filenames (without /dev/disk/by-id/ prefix).";
            };
          };
        };
        default = { };
        description = "Storage configuration for storage-pi hosts.";
      };
    };
  };
in
{
  options.lanbat = {
    profile = mkOption {
      type = types.str;
      internal = true;
      description = "Deployment profile name (site) this host belongs to.";
    };

    hostKey = mkOption {
      type = types.str;
      internal = true;
      description = "This host's key in deploy.nix hosts.";
    };

    deployment = {
      domain = mkOption {
        type = types.str;
        example = "home.example.com";
        description = "Base service domain. DNS for *.<domain> must point to the server.";
      };

      rootDomain = mkOption {
        type = types.str;
        example = "example.com";
        description = "Root DNS zone, typically the parent of deployment.domain.";
      };

      gatewayIp = mkOption {
        type = ipv4;
        example = "192.168.1.1";
        description = "Default gateway (usually the router).";
      };

      lanSubnet = mkOption {
        type = types.strMatching "([0-9]{1,3}\\.){3}[0-9]{1,3}/[0-9]{1,2}";
        example = "192.168.1.0/24";
        description = "LAN subnet in CIDR notation.";
      };

      nfsIdmapdDomain = mkOption {
        type = types.str;
        example = "home.lan";
        description = "NFSv4 ID mapping domain, identical on all NFS hosts.";
      };

      timezone = mkOption {
        type = types.str;
        example = "Europe/London";
        description = "System timezone of all hosts.";
      };

      phoneRegion = mkOption {
        type = types.strMatching "[A-Z]{2}";
        example = "GB";
        description = "ISO 3166-1 alpha-2 country code for phone number formatting.";
      };

      haLatitude = mkOption {
        type = types.str;
        example = "51.5";
        description = "Home latitude in decimal degrees.";
      };

      haLongitude = mkOption {
        type = types.str;
        example = "-0.1";
        description = "Home longitude in decimal degrees.";
      };

      haElevation = mkOption {
        type = types.int;
        example = 50;
        description = "Home elevation above sea level in metres.";
      };

      zigbeeVendorId = mkOption {
        type = types.strMatching "[0-9a-f]{4}";
        example = "10c4";
        description = "USB vendor ID of the Zigbee dongle.";
      };

      zigbeeProductId = mkOption {
        type = types.strMatching "[0-9a-f]{4}";
        example = "ea60";
        description = "USB product ID of the Zigbee dongle.";
      };

      adminSshKey = mkOption {
        type = types.strMatching "(ssh-|ecdsa-|sk-).+";
        example = "ssh-ed25519 AAAAC3Nza... admin@workstation";
        description = "SSH public key of the admin user on all hosts.";
      };

      overlay = mkOption {
        type = types.submodule {
          options = {
            provider = mkOption {
              type = types.str;
              default = "none";
              example = "wireguard-mesh";
              description = ''
                Which implementation answers the overlay contract. "none" keeps
                cross-host traffic on the LAN, which is what a profile that says
                nothing gets.
              '';
            };
            subnet = mkOption {
              type = types.nullOr types.str;
              default = null;
              example = "10.100.0.0/24";
              description = "Address range the overlay uses, when it has one.";
            };
            domain = mkOption {
              type = types.nullOr types.str;
              default = null;
              example = "lanbat.internal";
              description = "Suffix overlay names are resolved under, when the provider uses one.";
            };
          };
        };
        default = { };
        description = ''
          How hosts reach each other. Optional: absent means no overlay, and
          cross-host traffic stays on the LAN as it does today.
        '';
      };

      secrets = mkOption {
        type = types.submodule {
          options = {
            provider = mkOption {
              type = types.enum [
                "agenix"
                "sops"
                "none"
              ];
              example = "agenix";
              description = ''
                Backend that decrypts this profile's secrets. none resolves
                every requirement to a throwaway file, so evaluation and the
                flake checks need no encrypted files at all.
              '';
            };
            root = mkOption {
              type = types.path;
              example = lib.literalExpression "./secrets";
              description = ''
                Directory holding this profile's encrypted secrets, resolved
                relative to the profile rather than to lanbat itself, so a fork
                keeps its own secrets outside this repository.
              '';
            };
          };
        };
        description = "Where this profile's secrets come from and how they are decrypted.";
      };

      immich.adminEmail = mkOption {
        type = types.str;
        example = "alice@example.com";
        description = "Email for the bootstrap Immich admin.";
      };

      haLlm = mkOption {
        type = types.nullOr (
          types.submodule {
            options = {
              baseUrl = mkOption {
                type = types.strMatching "https?://.+";
                example = "https://api.runpod.ai/v2/<endpoint-id>/openai/v1";
                description = "Base URL of the OpenAI-compatible API.";
              };
              model = mkOption {
                type = types.str;
                example = "qwen3-8b-ha";
                description = "Name of the model the API serves.";
              };
            };
          }
        );
        default = null;
        description = "Home Assistant conversation agent LLM. null uses HA's own agent.";
      };

      parkingGuard = mkOption {
        type = types.nullOr (
          types.submodule {
            options = {
              siteId = mkOption {
                type = types.str;
                description = "JustPark site / location identifier for API sync.";
              };
              cameras = mkOption {
                type = types.listOf types.str;
                default = [ "c1" ];
                description = "Frigate camera names to evaluate for LPR.";
              };
              graceMinutes = mkOption {
                type = types.ints.positive;
                default = 5;
                description = "Minutes before booking start / after end to still treat as authorized.";
              };
              cooldownMinutes = mkOption {
                type = types.ints.positive;
                default = 30;
                description = "Minutes between repeat alerts for the same plate.";
              };
              minScore = mkOption {
                type = types.float;
                default = 0.8;
                description = "Minimum Frigate LPR confidence score to evaluate.";
              };
              residentPlates = mkOption {
                type = types.listOf types.str;
                default = [ ];
                description = "Always-authorized resident plates (manual allowlist).";
              };
              syncIntervalMinutes = mkOption {
                type = types.ints.positive;
                default = 5;
                description = "How often to poll JustPark / re-import CSV.";
              };
            };
          }
        );
        default = null;
        description = "Parking guard settings when lanbat-justpark-parking is enabled on the server.";
      };

      androidDevices = mkOption {
        type = types.attrsOf types.anything;
        default = { };
        description = ''
          Android TV boxes to provision, for the lanbat-android plugin.  The
          schema is declared by modules/server/android-devices.nix; this option
          only carries the values from deploy.nix, which is where real device
          addresses belong.  Type checking happens at the androidDevices option.
        '';
      };

      voiceRooms = mkOption {
        type = types.attrsOf types.str;
        default = { };
        example = {
          "Office" = "server";
          "Living Room" = "pi-storage";
        };
        description = ''
          Maps Home Assistant area names to host keys. Voice satellites on those
          hosts speak replies on the area's Music Assistant players.
        '';
      };

      primaryServer = mkOption {
        type = types.nullOr types.str;
        default = null;
        description = "Host key for the primary server. Required when multiple server-role hosts exist.";
      };

      primaryStorage = mkOption {
        type = types.nullOr types.str;
        default = null;
        description = "Host key for the primary storage-pi. Required when multiple storage-pi hosts exist.";
      };

      serverIp = mkOption {
        type = types.nullOr ipv4;
        readOnly = true;
        description = "IPv4 of primaryServer, computed from lanbat.hosts.";
      };

      storageIp = mkOption {
        type = types.nullOr ipv4;
        readOnly = true;
        description = "IPv4 of primaryStorage, computed from lanbat.hosts.";
      };

      storageHostname = mkOption {
        type = types.nullOr types.str;
        readOnly = true;
        description = "Hostname of primaryStorage, computed from lanbat.hosts.";
      };
    };

    hosts = mkOption {
      type = types.attrsOf hostSubmodule;
      description = "All hosts in this deployment.";
    };

    mosquitto = {
      extraUsers = mkOption {
        type = types.attrsOf (
          types.submodule {
            options = {
              passwordFile = mkOption {
                type = types.path;
                description = "Plaintext MQTT password file for this user.";
              };
              acl = mkOption {
                type = types.listOf types.str;
                default = [ "readwrite #" ];
                description = "Mosquitto ACL lines for this user.";
              };
            };
          }
        );
        default = { };
        description = "Extra Mosquitto users registered by plugins.";
      };
    };
  };
}
