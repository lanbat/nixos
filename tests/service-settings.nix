# tests/service-settings.nix
#
# Service settings schemas other than Frigate's (tests/frigate-settings.nix).
# Pure evaluation of the example profile, and of variants of it whose server
# carries one more module, as a profile's hosts.<key>.modules would:
#
#   - Samba: the default share layout renders as before; a profile adds,
#     changes and drops shares field by field, smbd binds to the NFS mounts of
#     exactly the drives the shares use, and an unknown key is rejected.
#   - Wyoming: the defaults keep today's pipeline and server satellite, and a
#     profile's voice, model and ALSA card reach the servers, the satellite and
#     Home Assistant's pipeline.
#   - Home Assistant: the loopback URLs of the services it wires come from
#     their descriptions, and the Zigbee2MQTT bridge watch follows whether
#     Zigbee2MQTT runs on the host unless the profile says otherwise.
#   - Telegraf: InfluxDB, Redis and the health checks are reached at the ports
#     the services' descriptions give, and a profile sets the ping targets.
#   - RomM: the library and the browser's arcade copies default to drive b's
#     media/roms and media/roms-browser/mame, a profile moves them, and the
#     NFS dependency follows the drive.
#   - Music Assistant: its setup reaches Music Assistant and Home Assistant at
#     the ports and subdomains of their descriptions. The fanart.tv VIP key is
#     off and needs no key by default; settings.fanartTvVip requires
#     ma-fanarttv-key and hands it to the setup.
#   - Immich: the originals default to drive a's photos, a profile moves them
#     and the NFS dependency follows; PostgreSQL, Redis and its own API are
#     reached at the ports of their descriptions.
#   - Grafana: its InfluxDB datasource follows InfluxDB's endpoint port.
#   - Nextcloud: the bulk data directories default to drive b's nextcloud,
#     and a profile moves them.
#   - Jellyfin: IMVDb is off and needs no key by default; settings.imvdb
#     requires jellyfin-imvdb-env and hands it to jellyfin-bootstrap.
#   - Audiobookshelf: the library defaults to drive b's media/audiobooks and
#     Audible, a profile moves it and picks another provider, and the NFS
#     dependency follows the drive.
#   - The Redis index registry keeps today's indexes and rejects a clash, and
#     Nextcloud's database is the workload instance's "nextcloud" over its
#     socket, as database.createLocally made it.
#
# Split into parts, one check each (service-settings, -2, -3): every variant is
# a whole profile evaluation, and the whole list in one process grew past the
# CI runner's memory (19.7 GB measured, 2026-10-06; CI builds each check in a
# process of its own). Part n takes every parts-th case from the n-th; the
# list is lazy, so a part evaluates only the variants its own cases use.
{
  lib,
  pkgs,
  inputs,
  self,
  agenix,
  disko,
  deploy-rs,
  nixpkgs,
  nixos-raspberrypi,
  part ? 1,
  parts ? 1,
}:

let
  inputsWithSelf = inputs // {
    self = self // {
      lanbatPlugins = import ../plugins;
    };
  };

  exampleDeploy = import ../deployments/example/deploy.nix { inputs = inputsWithSelf; };

  lanbatLib = import ../lib {
    self = inputsWithSelf.self;
    inputs = inputsWithSelf;
    profiles = { };
    inherit
      nixpkgs
      nixos-raspberrypi
      agenix
      disko
      deploy-rs
      ;
  };

  # The example server's configuration, with extra modules merged last.
  serverWith =
    modules:
    let
      deploy = exampleDeploy // {
        hosts = exampleDeploy.hosts // {
          server = exampleDeploy.hosts.server // {
            modules = (exampleDeploy.hosts.server.modules or [ ]) ++ modules;
          };
        };
      };
    in
    (lanbatLib.mkProfile "example" deploy).configurations.example-server.config;

  # Merge modules into named hosts (voice-satellite backend must match on every host).
  hostsWithModules =
    hostModules:
    exampleDeploy
    // {
      hosts = lib.mapAttrs (
        hostName: host:
        host
        // lib.optionalAttrs (hostModules ? ${hostName}) {
          modules = (host.modules or [ ]) ++ hostModules.${hostName};
        }
      ) exampleDeploy.hosts;
    };

  failedAssertions = config: map (a: a.message) (lib.filter (a: !a.assertion) config.assertions);

  base = serverWith [ ];

  # ── Jackett ──────────────────────────────────────────────────────────────
  jackett = base.lanbat.services.jackett;
  jackettService = base.services.jackett;
  jackettUnit = base.systemd.services.jackett;
  jackettPluginUnit = base.systemd.services.jackett-qbittorrent-plugin;
  jackettDefinitionsUnit = base.systemd.services.jackett-definitions;
  jackettDefinitionsTimer = base.systemd.timers.jackett-definitions;
  qbittorrentUnit = base.systemd.services.podman-qbittorrent;

  # ── Samba ────────────────────────────────────────────────────────────────
  smb = config: config.services.samba.settings;
  sambaDrives = config: config.lanbat.services.samba.nfs.drives;

  sambaChanged = serverWith [
    {
      lanbat.services.samba.settings = {
        workgroup = "LAB";
        shares = {
          private.enable = false;
          # Only the mode: the default owner and group stay.
          shared.createDirectory.mode = "0770";
          media-b.vetoFiles = [
            "adult"
            "tmp"
          ];
          scans = {
            drive = "a";
            path = "scans";
            readOnly = false;
            validUsers = [ "@media" ];
            extraConfig."hide dot files" = "yes";
          };
        };
      };
    }
  ];

  sambaDriveA = serverWith [
    {
      lanbat.services.samba.settings = {
        homes = false;
        shares = {
          media-b.enable = false;
          private.enable = false;
          shared.enable = false;
        };
      };
    }
  ];

  sambaTypo = serverWith [ { lanbat.services.samba.settings.share = { }; } ];

  # ── Wyoming ──────────────────────────────────────────────────────────────
  wyomingChanged = serverWith [
    {
      lanbat.services.wyoming.settings = {
        wakeWord.threshold = 0.5;
        speechToText = {
          model = "small-int8";
          language = "de";
        };
        textToSpeech.voice = "de_DE-thorsten-medium";
        satellite = {
          name = "Office Satellite";
          speaker = "plughw:CARD=Generic,DEV=0";
          mixer = [ ];
          microphoneUsbId = "046d:0825";
        };
      };
    }
  ];

  wyomingBadVoice = serverWith [ { lanbat.services.wyoming.settings.textToSpeech.voice = "alan"; } ];

  lvaServer =
    (lanbatLib.mkProfile "example" (hostsWithModules {
      server = [
        {
          lanbat.voiceSatellite = {
            backend = "lva";
            lva.continueConversationDelay = 0.8;
          };
        }
      ];
      pi-storage = [
        { lanbat.voiceSatellite.backend = "lva"; }
      ];
      pi-voice = [
        { lanbat.voiceSatellite.backend = "lva"; }
      ];
    })).configurations.example-server.config;

  # The example server's LVA satellite with these settings on top.
  lvaServerWith =
    settings:
    (lanbatLib.mkProfile "example" (hostsWithModules {
      server = [
        {
          lanbat.voiceSatellite = {
            backend = "lva";
          }
          // settings;
        }
      ];
      pi-storage = [ { lanbat.voiceSatellite.backend = "lva"; } ];
      pi-voice = [ { lanbat.voiceSatellite.backend = "lva"; } ];
    })).configurations.example-server.config;
  lvaTwoWords = lvaServerWith {
    lva.wakeModels = [
      "okay_nabu"
      "hey_nabu"
    ];
  };
  lvaOldOption = lvaServerWith { lva.wakeModel = "hey_jarvis"; };
  lvaThreeWords = lvaServerWith {
    lva.wakeModels = [
      "okay_nabu"
      "hey_jarvis"
      "alexa"
    ];
  };
  # The example Pi 3 on LVA, with echo cancellation and the music sent
  # through it.
  lvaPi3Aec =
    (lanbatLib.mkProfile "example" (hostsWithModules {
      server = [ { lanbat.voiceSatellite.backend = "lva"; } ];
      pi-storage = [ { lanbat.voiceSatellite.backend = "lva"; } ];
      pi-voice = [
        {
          lanbat.voiceSatellite = {
            backend = "lva";
            echoCancellation = {
              enable = true;
              includeMusic = true;
            };
          };
        }
      ];
    })).configurations.example-pi-voice.config;
  # The example TV box (pi-storage, lanbat-tv plugin) with an LVA satellite.
  lvaTvBox =
    (lanbatLib.mkProfile "example" (hostsWithModules {
      server = [ { lanbat.voiceSatellite.backend = "lva"; } ];
      pi-storage = [
        {
          lanbat.voiceSatellite.backend = "lva";
          lanbat.voiceSatellite.lva.volume = 0.4;
        }
      ];
      pi-voice = [ { lanbat.voiceSatellite.backend = "lva"; } ];
    })).configurations.example-pi-storage.config;
  # The TV box with its shows in another directory.
  tvShowsElsewhere =
    (lanbatLib.mkProfile "example" (hostsWithModules {
      pi-storage = [
        { lanbat.services.kodi.settings.videoSources.tv.path = "/mnt/storage-a/media/tv/shows/"; }
      ];
    })).configurations.example-pi-storage.config;
  # The example Android TV box with apps to open by voice.
  tvApps = serverWith [
    {
      androidDevices.bedroom = {
        room = "Living Room";
        apps = {
          Netflix = "com.netflix.ninja";
          YouTube = "com.google.android.youtube.tv";
          "YouTube Music" = "com.google.android.youtube.tvmusic";
        };
      };
    }
  ];
  automationById = config: id: lib.findFirst (a: a.id == id) null (haConfig config).automation;
  lvaExec = config: config.systemd.services.linux-voice-assistant.serviceConfig.ExecStart;
  lvaPre = config: config.systemd.services.linux-voice-assistant.serviceConfig.ExecStartPre;

  postSetup = config: config.systemd.services.home-assistant-post-setup.script;

  # ── Home Assistant ───────────────────────────────────────────────────────
  haConfig = config: config.services.home-assistant.config;
  # Dashboards are in storage mode, generated by post-setup.
  haYamlDashboard = config: config.services.home-assistant.lovelaceConfig != null;

  haMoved = serverWith [
    {
      lanbat.services = {
        home-assistant.port = lib.mkForce 18123;
        frigate.port = lib.mkForce 15000;
        music-assistant.port = lib.mkForce 18095;
        mosquitto.endpoint.port = lib.mkForce 11883;
      };
    }
  ];

  haNoZigbee = serverWith [
    { lanbat.services.home-assistant.settings.zigbee2mqttBridge = false; }
  ];

  # ── Jellyfin ─────────────────────────────────────────────────────────────
  jellyfinBootstrap = config: config.systemd.services.jellyfin-bootstrap.script;

  jellyfinImvdb = serverWith [ { lanbat.services.jellyfin.settings.imvdb = true; } ];

  # ── Audiobookshelf ───────────────────────────────────────────────────────
  absEnv = config: config.systemd.services.audiobookshelf-bootstrap.environment;

  absMoved = serverWith [
    {
      lanbat.services.audiobookshelf.settings = {
        drive = "a";
        libraryPath = "books/audio";
        metadataProvider = "audible.uk";
      };
    }
  ];

  # ── Redis indexes and Nextcloud's database ───────────────────────────────
  redisClash = serverWith [ { lanbat.redis.databases.other.index = 1; } ];

  envOf = config: container: config.virtualisation.oci-containers.containers.${container}.environment;
  imageOf = config: container: config.virtualisation.oci-containers.containers.${container}.image;
  optionsOf =
    config: container: config.virtualisation.oci-containers.containers.${container}.extraOptions;

  # ── Telegraf ─────────────────────────────────────────────────────────────
  telegrafConf = config: config.services.telegraf.extraConfig;
  healthChecks =
    config: map (c: "${c.name_override} ${lib.head c.urls}") (telegrafConf config).inputs.http_response;

  telegrafChanged = serverWith [
    {
      lanbat.services = {
        influxdb.endpoint.port = lib.mkForce 18086;
        grafana.port = lib.mkForce 13030;
        telegraf.settings.pingTargets = [ "192.0.2.1" ];
      };
      services.redis.servers.shared.port = lib.mkForce 16379;
    }
  ];

  # ── RomM ─────────────────────────────────────────────────────────────────
  rommVolumes = config: config.virtualisation.oci-containers.containers.romm.volumes;

  rommMoved = serverWith [
    {
      lanbat.services.romm.settings = {
        drive = "a";
        libraryPath = "games/roms";
        browserArcadePath = "games/arcade";
        authentikAdmin = "alice";
      };
    }
  ];

  # ── Music Assistant ──────────────────────────────────────────────────────
  maScript = config: config.systemd.services.music-assistant-setup.script;

  maMoved = serverWith [
    {
      lanbat.services = {
        music-assistant.port = lib.mkForce 18095;
        home-assistant = {
          port = lib.mkForce 18123;
          subdomain = lib.mkForce "hass";
        };
      };
    }
  ];

  maFanartVip = serverWith [
    { lanbat.services.music-assistant.settings.fanartTvVip = true; }
  ];

  # ── Snapcast ─────────────────────────────────────────────────────────────
  fwStart = config: config.networking.firewall.extraCommands;
  fwStop = config: config.networking.firewall.extraStopCommands;
  admits =
    address: config:
    lib.all
      (
        port: lib.hasInfix "INPUT -p tcp --dport ${toString port} -s ${address} -j ACCEPT" (fwStart config)
      )
      [
        1704
        1705
      ];
  snapClients = serverWith [
    { lanbat.services.snapcast.settings.clients.phone.host = "192.0.2.70"; }
  ];
  snapBadHost = serverWith [
    { lanbat.services.snapcast.settings.clients.phone.host = "phone.lan"; }
  ];
  snapDuplicate = serverWith [
    {
      lanbat.services.snapcast.settings.clients = {
        phone.host = "192.0.2.70";
        tablet.host = "192.0.2.70";
      };
    }
  ];
  snapMac = serverWith [
    { lanbat.services.snapcast.settings.clients.tv.mac = "2c:d8:ae:00:00:01"; }
  ];
  snapEmpty = serverWith [ { lanbat.services.snapcast.settings.clients.tv = { }; } ];
  snapBadMac = serverWith [ { lanbat.services.snapcast.settings.clients.tv.mac = "2c-d8-ae"; } ];
  # Music Assistant placed on a host without the snapserver.
  maApart = serverWith [ { lanbat.endpoints.snapcast.hosts = lib.mkForce [ "pi-storage" ]; } ];

  # ── Immich ───────────────────────────────────────────────────────────────
  immichMoved = serverWith [
    {
      lanbat.services.immich = {
        port = lib.mkForce 12283;
        settings = {
          drive = "b";
          uploadPath = "media/photos";
        };
      };
      services.redis.servers.shared.port = lib.mkForce 16379;
    }
  ];
  immichUpload =
    config: lib.head config.virtualisation.oci-containers.containers.immich-server.volumes;

  # ── Nextcloud ────────────────────────────────────────────────────────────
  nextcloudMoved = serverWith [
    {
      lanbat.services.nextcloud.settings.storage = {
        drive = "a";
        path = "cloud";
      };
    }
  ];
  nextcloudDirs =
    config: lib.filter (lib.hasInfix " nextcloud nextcloud ") config.systemd.tmpfiles.rules;

  expect = name: ok: if ok then null else name;

  cases = [
    (expect "samba: the default layout renders today's shares" (
      smb base == {
        global = (smb base).global;
        homes = {
          comment = "Home Directories";
          browseable = "no";
          "read only" = "no";
          "create mask" = "0700";
          "directory mask" = "0700";
          "valid users" = "%S";
          path = "/srv/storage/b/users/%S/files";
        };
        media = {
          comment = "Media: movies, TV, music videos";
          path = "/srv/storage/a/media";
          browseable = "yes";
          "read only" = "yes";
          "guest ok" = "no";
          "valid users" = "@media";
        };
        media-b = {
          comment = "Media: music, documentaries, books, ROMs";
          path = "/srv/storage/b/media";
          browseable = "yes";
          "read only" = "yes";
          "guest ok" = "no";
          "valid users" = "@media";
          "veto files" = "/adult/";
        };
        private = {
          comment = "Private";
          path = "/srv/storage/b/media/adult";
          browseable = "no";
          "read only" = "yes";
          "guest ok" = "no";
          "valid users" = "@private";
        };
        shared = {
          comment = "Shared";
          path = "/srv/storage/b/shared";
          browseable = "yes";
          "read only" = "no";
          "guest ok" = "no";
          "valid users" = "@media";
          "create mask" = "0664";
          "directory mask" = "0775";
          "force group" = "media";
        };
      }
      && (smb base).global.workgroup == "WORKGROUP"
      && (smb base).global."netbios name" == "server"
      &&
        sambaDrives base == [
          "a"
          "b"
        ]
      && lib.elem "d /srv/storage/b/shared 0775 root media -" base.systemd.tmpfiles.rules
    ))

    (expect "samba: a profile adds, changes and drops shares field by field" (
      let
        s = smb sambaChanged;
      in
      s.global.workgroup == "LAB"
      && !(s ? private)
      && s.media-b."veto files" == "/adult/tmp/"
      && s.media-b.comment == "Media: music, documentaries, books, ROMs"
      &&
        s.scans == {
          comment = "";
          path = "/srv/storage/a/scans";
          browseable = "yes";
          "read only" = "no";
          "guest ok" = "no";
          "valid users" = "@media";
          "hide dot files" = "yes";
        }
      && lib.elem "d /srv/storage/b/shared 0770 root media -" sambaChanged.systemd.tmpfiles.rules
    ))

    (expect "samba: smbd binds to the drives its shares use" (
      sambaDrives sambaDriveA == [ "a" ]
      && !((smb sambaDriveA) ? homes)
      &&
        lib.attrNames (smb sambaDriveA) == [
          "global"
          "media"
        ]
    ))

    (expect "samba: an unknown settings key is rejected" (
      lib.any (lib.hasInfix "samba has no setting \"share\"") (failedAssertions sambaTypo)
    ))

    (expect "wyoming: the defaults keep today's pipeline and satellite" (
      let
        w = base.services.wyoming;
        sat = base.lanbat.voiceSatellite;
      in
      w.openwakeword.threshold == toString 0.35
      && w.faster-whisper.servers.main.model == "base-int8"
      && w.faster-whisper.servers.main.language == "en"
      && w.piper.servers.main.voice == "en_GB-alan-medium"
      && sat.name == "Server Satellite"
      && sat.speaker == "plughw:CARD=PCH,DEV=0"
      && sat.mixer == [ "-c PCH sset Master 80% unmute" ]
      && sat.microphone.usbId == "1415:2000"
    ))

    (expect "wyoming: a profile's settings reach the servers, satellite and pipeline" (
      let
        w = wyomingChanged.services.wyoming;
        sat = wyomingChanged.lanbat.voiceSatellite;
      in
      w.openwakeword.threshold == toString 0.5
      && w.faster-whisper.servers.main.model == "small-int8"
      && w.piper.servers.main.voice == "de_DE-thorsten-medium"
      && sat.name == "Office Satellite"
      && sat.speaker == "plughw:CARD=Generic,DEV=0"
      && sat.mixer == [ ]
      && sat.microphone.usbId == "046d:0825"
      && lib.hasInfix ''PIPELINE_STT_LANGUAGE="de"'' (postSetup wyomingChanged)
      && lib.hasInfix ''PIPELINE_TTS_LANGUAGE="de_DE"'' (postSetup wyomingChanged)
    ))

    (expect "wyoming: a voice without a language prefix is rejected" (
      !(builtins.tryEval (
        builtins.deepSeq wyomingBadVoice.services.wyoming.piper.servers.main.voice true
      )).success
    ))

    (expect "lva: backend runs Linux Voice Assistant and registers ESPHome in post-setup" (
      let
        exec = lvaServer.systemd.services.linux-voice-assistant.serviceConfig.ExecStart or "";
        setup = postSetup lvaServer;
      in
      !(lvaServer.services.wyoming.satellite.enable or false)
      && lib.hasInfix "linux-voice-assistant" exec
      && lib.hasInfix "--wake-model" exec
      && lib.hasInfix "--wake-model okay_nabu" exec
      && lib.hasInfix "--stop-model" exec
      && lib.hasInfix "--continue-conversation-delay" exec
      && lib.hasInfix "0.8" exec
      && lib.hasInfix "VOICE_SATELLITE_REGISTRATIONS" setup
      && lib.hasInfix "|lva|Server Satellite" setup
      && lib.elem "esphome" lvaServer.services.home-assistant.extraComponents
    ))

    # LVA's audio library talks only to a PulseAudio server: a server without
    # one gets system-wide PipeWire, its mixer settings before LVA starts, and
    # the microphone's mono capture node.
    (expect "lva: a server gets system-wide PipeWire with a PulseAudio server" (
      lvaServer.services.pipewire.enable
      && lvaServer.services.pipewire.systemWide
      && lvaServer.services.pipewire.pulse.enable
    ))

    (expect "lva: the server's mixer settings run before LVA" (
      # systemd reads % as a specifier, so the command line carries %%.
      lib.any (lib.hasInfix "sset Master 80%% unmute") (
        lvaServer.systemd.services.linux-voice-assistant.serviceConfig.ExecStartPre or [ ]
      )
    ))

    (expect "lva: the microphone gets its mono capture node" (
      lvaServer.services.pipewire.wireplumber.extraConfig ? "52-lva-microphone"
    ))

    (expect "lva: the server passes its assertions" (failedAssertions lvaServer == [ ]))

    # LVA satellites fetch replies from internal_url, so it must be reachable
    # from other hosts: the HTTPS address through Caddy, and the player gets
    # the profile's CA to verify it.
    (expect "lva: internal_url is the HTTPS address, and LVA verifies it" (
      (haConfig lvaServer).homeassistant.internal_url == "https://ha.home.example.com"
      && lvaServer.systemd.services.linux-voice-assistant.environment ? LVA_TLS_CA_FILE
    ))

    # "play ..." on the room's speaker, and each satellite's room and host name
    # for post-setup's area step (the example puts the server in the Office).
    (expect "lva: voice play and stop automations, and rooms in the registrations" (
      lib.all (id: lib.elem id (map (a: a.id) (haConfig lvaServer).automation)) [
        "lanbat_voice_play"
        "lanbat_voice_stop"
      ]
      && lib.hasInfix "|lva|Server Satellite|Office|" (postSetup lvaServer)
    ))

    (expect "lva: the music ducks to near silence while listening, a quarter while answering" (
      lvaPi3Aec.systemd.services.lva-snapcast-duck.environment.DUCK_LISTEN_VOLUME == "0.05"
      # A ducker restarted with the music down finds the volumes to put back.
      && lvaPi3Aec.systemd.services.lva-snapcast-duck.environment ? STATE_FILE
      && lvaPi3Aec.systemd.services.lva-snapcast-duck.serviceConfig.RuntimeDirectoryPreserve
      # And WirePlumber doesn't give a new music stream the ducked volume.
      && lvaPi3Aec.services.pipewire.wireplumber.extraConfig ? "51-snapclient-volume"
      && lvaPi3Aec.systemd.services.lva-snapcast-duck.environment.DUCK_VOLUME == "0.25"
    ))

    # A WirePlumber or PipeWire restart recreates the microphone's node; LVA
    # must restart with them or it stays deaf.
    (expect "lva: restarts with the audio stack" (
      lib.all (u: lib.elem u lvaPi3Aec.systemd.services.linux-voice-assistant.partOf) [
        "pipewire.service"
        "wireplumber.service"
      ]
    ))

    (expect "lva: includeMusic plays Snapcast into the echo-cancel sink" (
      lib.hasInfix "--soundcard lanbat_aec_playback" lvaPi3Aec.systemd.services.snapclient.serviceConfig.ExecStart
      && !(lib.hasInfix "--soundcard" lvaServer.systemd.services.snapclient.serviceConfig.ExecStart or "")
    ))

    (expect "lva: replies play into the echo-cancel sink through mpv's pulse driver" (
      lib.hasInfix "--audio-output-device pulse/lanbat_aec_playback" lvaPi3Aec.systemd.services.linux-voice-assistant.serviceConfig.ExecStart
    ))

    (expect "lva: stop works at the end of a transcript that caught the radio" (
      let
        stop = lib.findFirst (a: a.id == "lanbat_voice_stop") null (haConfig lvaServer).automation;
      in
      lib.elem "{noise} (stop|turn off) the (music|radio|podcast|audiobook|player|speaker)" (lib.head stop.trigger)
      .command
    ))

    # Speech-to-text goes through the speaker-identification proxy, which
    # passes it on to faster-whisper; faster-whisper's entry stays.
    (expect "voice-id: the pipeline's speech-to-text, in front of faster-whisper" (
      base.systemd.services ? voice-id
      && lib.hasInfix "--upstream tcp://127.0.0.1:10301" base.systemd.services.voice-id.serviceConfig.ExecStart
      && lib.hasInfix ''export PIPELINE_STT_ENGINE="stt.voice_id"'' (postSetup base)
      && lib.elem 10303 base.lanbat.services.voice-id.extraPorts
    ))

    # A "stop" step inside if/then throws away the reply set before it (HA's
    # _StopScript skips copying the sub-script's response), so the automations
    # branch with if/then/else instead and every branch ends with a reply.
    (expect "voice automations never use stop, so their replies reach the satellite" (
      let
        voiceAutomations = lib.filter (a: lib.hasPrefix "lanbat_voice" a.id) (haConfig base).automation;
      in
      voiceAutomations != [ ]
      && !(lib.any (a: lib.hasInfix "\"stop\":" (builtins.toJSON a.action)) voiceAutomations)
    ))

    # Any host with an LVA satellite plays music in its room: a Snapcast client
    # (a Music Assistant player) and the ducker, like the Pis.
    (expect "lva: a server satellite is also a music player, with ducking" (
      lvaServer.lanbat.snapclient.enable
      && lvaServer.systemd.services ? snapclient
      && lvaServer.systemd.services ? lva-snapcast-duck
      && lvaServer.services.pipewire.wireplumber.extraConfig ? "51-snapclient-volume"
    ))

    (expect "lva: playMusic = false keeps a satellite to spoken replies" (
      !(lvaServerWith { playMusic = false; }).lanbat.snapclient.enable
    ))

    (expect "lva: the wake words are set in prefs.json on every start" (
      lib.any (lib.hasInfix "lva-set-prefs") (lvaPre lvaServer)
      && lvaServer.lanbat.voiceSatellite.lva.wakeModels == [ "okay_nabu" ]
    ))

    (expect "lva: two wake words; the first is --wake-model, hey_nabu brings its model" (
      lvaTwoWords.lanbat.voiceSatellite.lva.wakeModels == [
        "okay_nabu"
        "hey_nabu"
      ]
      && lib.hasInfix "--wake-model okay_nabu" (lvaExec lvaTwoWords)
      && lib.hasInfix "lva-wakewords-hey-nabu" (lvaExec lvaTwoWords)
    ))

    (expect "lva: the old wakeModel option still works, with a warning" (
      lvaOldOption.lanbat.voiceSatellite.lva.wakeModels == [ "hey_jarvis" ]
      && lib.hasInfix "--wake-model hey_jarvis" (lvaExec lvaOldOption)
      && lib.any (lib.hasInfix "wakeModel is deprecated") lvaOldOption.warnings
    ))

    (expect "lva: more than two wake words are rejected" (
      !(builtins.tryEval (builtins.deepSeq lvaThreeWords.lanbat.voiceSatellite.lva.wakeModels true))
      .success
    ))

    (expect "tv: Home Assistant reaches each Kodi with its password and knows its room" (
      lib.all (name: lib.elem name base.lanbat.services.home-assistant.consumes) [
        "kodi"
        "kodi-events"
      ]
      && base.lanbat.services.home-assistant.secrets.kodi-web-password.owner == "hass"
      && lib.all (c: lib.elem c base.services.home-assistant.extraComponents) [
        "kodi"
        "androidtv"
      ]
      && lib.hasInfix "pi-storage|" (postSetup base)
      && lib.hasInfix "|Living Room|" (postSetup base)
      && lib.hasInfix "KODI_PASSWORD_FILE" (postSetup base)
      && lib.hasInfix "ANDROID_TVS" (postSetup base)
    ))

    (expect
      "tv: an LVA satellite on the TV box pauses Kodi, ducks its music and keeps the peripheral API"
      (
        lvaTvBox.systemd.services ? lva-kodi-companion
        && lvaTvBox.systemd.services.lva-kodi-companion.serviceConfig.DynamicUser
        && lvaTvBox.systemd.services.lva-kodi-companion.environment.KODI_PORT == "9090"
        && lvaTvBox.systemd.services.lva-snapcast-duck.environment.DUCK_BINARIES == "snapclient,kodi.bin"
        && lib.hasInfix "--peripheral-port" (lvaExec lvaTvBox)
        && !(base.systemd.services ? lva-kodi-companion)
      )
    )

    # The reply volume is set at every start, the wake words' way; without
    # the option, the volume Home Assistant last set stays.
    (expect "lva: the configured reply volume is written before LVA starts" (
      lib.any (lib.hasInfix "lva-set-prefs 0.4") lvaTvBox.systemd.services.linux-voice-assistant.serviceConfig.ExecStartPre
      && lib.any (lib.hasInfix "lva-set-prefs") lvaPi3Aec.systemd.services.linux-voice-assistant.serviceConfig.ExecStartPre
      && !lib.any (lib.hasInfix "lva-set-prefs ") lvaPi3Aec.systemd.services.linux-voice-assistant.serviceConfig.ExecStartPre
    ))

    # They stop with LVA (partOf); starting LVA again (a restart by hand, the
    # microphone plugged back in) must bring them back, or nothing pauses or
    # ducks until a reboot.
    (expect "lva: the ducker and the Kodi companion start with LVA" (
      lib.all (u: lib.elem "linux-voice-assistant.service" lvaTvBox.systemd.services.${u}.wantedBy) [
        "lva-snapcast-duck"
        "lva-kodi-companion"
      ]
    ))

    # Kodi's startup update rescans only folders it has scanned before; a music
    # source nothing scanned stayed empty (9,784 songs on the Pi 5, 2026-10-08).
    (expect "tv: Kodi's first music scan runs after Kodi starts" (
      let
        scan = lvaTvBox.systemd.services.kodi-music-scan;
      in
      lib.elem "tv-kodi.service" scan.wantedBy
      && lib.elem "tv-kodi.service" scan.after
      && lib.hasInfix "music-scan" scan.script
    ))

    # The library sources come from settings: the repository's layout by
    # default, and a profile changes one field without losing the others.
    (expect "tv: Kodi's library sources are settings, the layout by default" (
      let
        env = c: c.systemd.services.kodi-bootstrap.environment;
        lines = c: lib.splitString "\n" (env c).KODI_VIDEO_SOURCES;
      in
      lib.elem "tv|/mnt/storage-a/media/tv/|tvshows|metadata.tvshows.themoviedb.org.python|0|1" (
        lines lvaTvBox
      )
      && lib.elem "movies|/mnt/storage-a/media/movies/|movies|metadata.themoviedb.org.python|1|0" (
        lines lvaTvBox
      )
      && lib.elem "music-videos|/mnt/storage-a/media/music-videos/|musicvideos|metadata.local|1|0" (
        lines lvaTvBox
      )
      && lib.hasInfix "Music|/mnt/storage-b/media/music/" (env lvaTvBox).KODI_MUSIC_SOURCES
      && lib.elem "tv|/mnt/storage-a/media/tv/shows/|tvshows|metadata.tvshows.themoviedb.org.python|0|1" (
        lines tvShowsElsewhere
      )
      && lib.length (lines tvShowsElsewhere) == lib.length (lines lvaTvBox)
      && tvShowsElsewhere.systemd.services.kodi-music-scan.environment ? KODI_MUSIC_SOURCES
    ))

    (expect "tv: the companion rewinds a film it paused for a question" (
      lvaTvBox.systemd.services.lva-kodi-companion.environment.RESUME_REWIND_SECONDS == "3"
    ))

    (expect "tv: a new Kodi stream doesn't inherit a ducked volume" (
      lvaTvBox.services.pipewire.wireplumber.extraConfig ? "51-kodi-volume"
    ))

    (expect "tv: Kodi's power is switched over CEC, library calls go through an event" (
      (automationById base "lanbat_kodi_power").action != [ ]
      && lib.hasInfix "tv.{{ trigger.id }}" (
        builtins.toJSON (automationById base "lanbat_kodi_power").action
      )
      && lib.any (t: lib.elem "watch {query}" t.command) (automationById base "lanbat_voice_play").trigger
      && lib.hasInfix "VideoLibrary.GetEpisodes" (
        builtins.toJSON (automationById base "lanbat_voice_play")
      )
    ))

    (expect "tv: an Android box opens its own apps by name, longest first, and nothing else" (
      let
        tvTriggers = (automationById tvApps "lanbat_voice_tv").trigger;
        baseTv = (automationById base "lanbat_voice_tv").trigger;
      in
      lib.any (
        t: (t.id or "") == "open" && lib.hasInfix "(YouTube Music|Netflix|YouTube)" (lib.head t.command)
      ) tvTriggers
      && !lib.any (t: (t.id or "") == "open") baseTv
      && lib.hasInfix "|Living Room" (postSetup tvApps)
    ))

    # Seeking, subtitles, what's on, stopping and episodes act on the film or
    # show on the room's Kodi; none takes "play ...", which the video and
    # music requests own.
    (expect "tv: Kodi takes seek, subtitles, what's on, stop and episode requests" (
      let
        tvTriggers = (automationById base "lanbat_voice_tv").trigger;
        ids = map (t: t.id or "") tvTriggers;
        sentences = lib.concatMap (t: t.command or [ ]) tvTriggers;
      in
      lib.all (id: lib.elem id ids) [
        "seek"
        "subtitles"
        "whats_on"
        "stop_video"
        "episode"
      ]
      && !lib.any (lib.hasPrefix "[play]") sentences
      && !lib.any (lib.hasPrefix "play ") sentences
    ))

    # Home Assistant has no regex_escape filter; a template using one
    # disables its whole automation at load.
    (expect "voice automations use only filters Home Assistant has" (
      !lib.hasInfix "regex_escape" (builtins.toJSON (haConfig base).automation)
    ))

    (expect "home assistant: the defaults keep today's URLs and Zigbee watch" (
      (haConfig base).homeassistant.internal_url == "http://127.0.0.1:8123"
      && lib.hasInfix ''export FRIGATE_URL="http://127.0.0.1:5000/"'' (postSetup base)
      && lib.hasInfix ''export MUSIC_ASSISTANT_URL="http://127.0.0.1:8095"'' (postSetup base)
      && lib.hasInfix ''export MQTT_PORT="1883"'' (postSetup base)
      && base.lanbat.voiceSatellite.homeAssistant.url == "http://127.0.0.1:8123"
      &&
        map (a: a.id) (haConfig base).automation == [
          "lanbat_zigbee_bridge_offline"
          "lanbat_zigbee_bridge_online"
          "lanbat_voice_play"
          "lanbat_voice_stop"
          "lanbat_voice_volume"
          "lanbat_voice_tv"
          "lanbat_kodi_call"
          "lanbat_kodi_power"
        ]
      && !(haYamlDashboard base)
    ))

    (expect "home assistant: dashboards generated from the registries, rooms from deviceAreas" (
      lib.hasInfix "export DEVICE_AREAS=" (postSetup base)
      && lib.hasInfix "livingroom_lamp" (postSetup base)
      && lib.hasInfix "/bin/home-assistant-dashboards" (postSetup base)
      && lib.hasInfix ''"Frigate":"https://'' (postSetup base)
      && lib.hasInfix "https://grafana." (postSetup base)
      && base.systemd.timers.home-assistant-post-setup.timerConfig.OnCalendar != null
    ))

    (expect "home assistant: the URLs follow the services' descriptions" (
      (haConfig haMoved).homeassistant.internal_url == "http://127.0.0.1:18123"
      && lib.hasInfix ''export INTERNAL_URL="http://127.0.0.1:18123"'' haMoved.systemd.services.home-assistant-bootstrap.script
      && lib.hasInfix ''export FRIGATE_URL="http://127.0.0.1:15000/"'' (postSetup haMoved)
      && lib.hasInfix ''export MUSIC_ASSISTANT_URL="http://127.0.0.1:18095"'' (postSetup haMoved)
      && lib.hasInfix ''export MQTT_PORT="11883"'' (postSetup haMoved)
      && haMoved.lanbat.voiceSatellite.homeAssistant.url == "http://127.0.0.1:18123"
    ))

    (expect "home assistant: the Zigbee watch can be turned off" (
      map (a: a.id) (haConfig haNoZigbee).automation == [
        "lanbat_voice_play"
        "lanbat_voice_stop"
        "lanbat_voice_volume"
        "lanbat_voice_tv"
        "lanbat_kodi_call"
        "lanbat_kodi_power"
      ]
      && !(haYamlDashboard haNoZigbee)
    ))

    (expect "telegraf: the defaults keep today's outputs and inputs" (
      let
        t = telegrafConf base;
      in
      (lib.head t.outputs.influxdb_v2).urls == [ "http://localhost:8086" ]
      &&
        healthChecks base == [
          "grafana http://127.0.0.1:3030/api/health"
          "home-assistant http://127.0.0.1:8123/"
          "jellyfin http://127.0.0.1:8096/health"
          "immich http://127.0.0.1:2283/api/server/ping"
          "vaultwarden http://127.0.0.1:8222/alive"
        ]
      &&
        (lib.head t.inputs.ping).urls == [
          "192.0.2.11"
          "192.0.2.1"
          "1.1.1.1"
        ]
      && (lib.head t.inputs.redis).servers == [ "tcp://127.0.0.1:6379" ]
    ))

    (expect "telegraf: the ports follow the services and the ping targets the profile" (
      let
        t = telegrafConf telegrafChanged;
      in
      (lib.head t.outputs.influxdb_v2).urls == [ "http://localhost:18086" ]
      && lib.head (healthChecks telegrafChanged) == "grafana http://127.0.0.1:13030/api/health"
      && (lib.head t.inputs.ping).urls == [ "192.0.2.1" ]
      && (lib.head t.inputs.redis).servers == [ "tcp://127.0.0.1:16379" ]
    ))

    (expect "romm: the defaults keep today's library on drive b" (
      lib.sublist 3 2 (rommVolumes base) == [
        "/srv/storage/b/media/roms:/romm/library/roms"
        "/srv/storage/b/media/roms-browser/mame:/romm/library/roms/mame"
      ]
      && base.lanbat.services.romm.nfs.drives == [ "b" ]
      && base.lanbat.services.romm.workloadDirs.romm.mode == "0711"
      && base.lanbat.services.romm.workloadDirs."romm/resources".mode == "0711"
      &&
        base.systemd.services.romm-browser-romsets.environment.SOURCE_DIR
        == "/srv/storage/b/media/roms/mame"
      && (envOf base "romm").REDIS_PORT == "6379"
      && (envOf base "romm").OIDC_ALLOW_REGISTRATION == "false"
      && (envOf base "romm").DISABLE_USERPASS_LOGIN == "false"
      && lib.hasSuffix "/api/oauth/openid" (envOf base "romm").OIDC_REDIRECT_URI
      && lib.hasInfix "-v user=akadmin " base.systemd.services.romm-admin-email.script
      && lib.elem "romm-admin-email.service" base.systemd.services.podman-romm.wants
      && lib.elem "romm-admin-email" base.lanbat.services.romm.units
    ))

    (expect "romm: a profile moves the library, and the NFS dependency follows" (
      lib.sublist 3 2 (rommVolumes rommMoved) == [
        "/srv/storage/a/games/roms:/romm/library/roms"
        "/srv/storage/a/games/arcade:/romm/library/roms/mame"
      ]
      && rommMoved.lanbat.services.romm.nfs.drives == [ "a" ]
      &&
        rommMoved.systemd.services.romm-browser-romsets.environment.TARGET_DIR
        == "/srv/storage/a/games/arcade"
      && lib.hasInfix "-v user=alice " rommMoved.systemd.services.romm-admin-email.script
      && failedAssertions rommMoved == [ ]
    ))

    (expect "music-assistant: the setup keeps today's URLs" (
      lib.all (line: lib.hasInfix line (maScript base)) [
        ''export MA_URL="http://127.0.0.1:8095"''
        ''export MA_PUBLIC_URL="https://music.home.example.com"''
        ''export HA_INTERNAL_URL="http://127.0.0.1:8123"''
        ''export HA_PUBLIC_URL="https://ha.home.example.com"''
      ]
    ))

    (expect "music-assistant: the setup follows the services' ports and subdomains" (
      lib.all (line: lib.hasInfix line (maScript maMoved)) [
        ''export MA_URL="http://127.0.0.1:18095"''
        ''export HA_INTERNAL_URL="http://127.0.0.1:18123"''
        ''export HA_PUBLIC_URL="https://hass.home.example.com"''
      ]
    ))

    (expect "music-assistant: the fanart.tv VIP key is off by default and needs no key" (
      !(base.lanbat.secrets ? ma-fanarttv-key) && !lib.hasInfix "MA_FANARTTV_KEY" (maScript base)
    ))

    (expect "music-assistant: settings.fanartTvVip requires the key and hands it to the setup" (
      maFanartVip.lanbat.secrets ? ma-fanarttv-key
      && lib.hasInfix maFanartVip.lanbat.secrets.ma-fanarttv-key.path (maScript maFanartVip)
      && failedAssertions maFanartVip == [ ]
    ))

    (expect "immich: the defaults keep today's upload directory and ports" (
      immichUpload base == "/srv/storage/a/photos:/usr/src/app/upload"
      && base.lanbat.services.immich.nfs.drives == [ "a" ]
      && lib.elem "d /srv/storage/a/photos 0750 immich immich -" base.systemd.tmpfiles.rules
      && (envOf base "immich-server").DB_PORT == "5432"
      && (envOf base "immich-server").REDIS_PORT == "6379"
      && lib.hasInfix ''IMMICH_URL="http://127.0.0.1:2283"'' base.systemd.services.immich-bootstrap.script
      && lib.hasInfix ''"https://photos.home.example.com"'' base.systemd.services.podman-immich-server.preStart
    ))

    (expect "immich: images are pinned to the deployed v3.2.1 digests" (
      imageOf base "immich-server"
      == "ghcr.io/immich-app/immich-server:v3.2.1@sha256:87bb1b208434a8503e1a2465edd84f3cf94bd72c66feb7ca474629015b8dbfd6"
      &&
        imageOf base "immich-machine-learning"
        == "ghcr.io/immich-app/immich-machine-learning:v3.2.1@sha256:49a53dbf5fbea5c785075667056fb010498969b9143005df5434868035bf5654"
    ))

    (expect "immich: machine learning cannot exhaust the host" (
      optionsOf base "immich-machine-learning" == [
        "--network=host"
        "--memory=8g"
        "--memory-reservation=6g"
        "--memory-swap=10g"
        "--cpus=2"
      ]
    ))

    (expect "server: compressed swap and bounded core dumps preserve recovery headroom" (
      base.zramSwap.enable
      && base.zramSwap.memoryPercent == 25
      && base.zramSwap.memoryMax == 8 * 1024 * 1024 * 1024
      && base.zramSwap.priority == 100
      && lib.all (line: lib.hasInfix line base.environment.etc."systemd/coredump.conf".text) [
        "Storage=external"
        "ProcessSizeMax=1G"
        "ExternalSizeMax=1G"
        "MaxUse=2G"
        "KeepFree=15G"
      ]
    ))

    (expect "immich: a profile moves the uploads, and the ports follow the services" (
      immichUpload immichMoved == "/srv/storage/b/media/photos:/usr/src/app/upload"
      && immichMoved.lanbat.services.immich.nfs.drives == [ "b" ]
      && (envOf immichMoved "immich-server").REDIS_PORT == "16379"
      && lib.hasInfix ''IMMICH_URL="http://127.0.0.1:12283"'' immichMoved.systemd.services.immich-bootstrap.script
      && failedAssertions immichMoved == [ ]
    ))

    (expect "grafana: the InfluxDB datasource follows InfluxDB's endpoint" (
      let
        influxUrl =
          config: (lib.head config.services.grafana.provision.datasources.settings.datasources).url;
      in
      influxUrl base == "http://localhost:8086"
      && influxUrl telegrafChanged == "http://localhost:18086"
      && base.services.grafana.settings.server.root_url == "https://grafana.home.example.com"
    ))

    (expect "nextcloud: the bulk data directories keep today's paths, and move" (
      lib.all (rule: lib.elem rule (nextcloudDirs base)) [
        "d /srv/storage/b/nextcloud          0750 nextcloud nextcloud -"
        "d /srv/storage/b/nextcloud/external 0750 nextcloud nextcloud -"
        "d /srv/storage/b/nextcloud/users    0750 nextcloud nextcloud -"
      ]
      && lib.elem "d /srv/storage/a/cloud/users    0750 nextcloud nextcloud -" (
        nextcloudDirs nextcloudMoved
      )
      && !lib.any (lib.hasPrefix "d /srv/storage/b/nextcloud") (nextcloudDirs nextcloudMoved)
      && nextcloudMoved.lanbat.services.nextcloud.nfs.drives == [ ]
    ))

    (expect "redis: the consumers keep today's indexes" (
      lib.mapAttrs (_: db: db.index) base.lanbat.redis.databases == {
        authentik = 0;
        immich = 1;
        romm = 2;
      }
      && (envOf base "authentik-server").AUTHENTIK_REDIS__DB == "0"
      && (envOf base "immich-server").REDIS_DBINDEX == "1"
      && (envOf base "romm").REDIS_DB == "2"
    ))

    (expect "redis: two consumers claiming one index are rejected" (
      lib.any (lib.hasInfix "index 1 is claimed by immich and other") (failedAssertions redisClash)
    ))

    (expect "nextcloud: the workload instance's nextcloud database, over its socket" (
      let
        nc = base.services.nextcloud;
        pg = base.services.postgresql;
      in
      !nc.database.createLocally
      && nc.config.dbhost == "/run/postgresql"
      && nc.config.dbname == "nextcloud"
      && nc.config.dbuser == "nextcloud"
      && nc.config.dbpassFile == null
      && base.lanbat.postgresql.databases.nextcloud.instance == "workload"
      && lib.elem "nextcloud" pg.ensureDatabases
      && lib.any (u: u.name == "nextcloud" && u.ensureDBOwnership) pg.ensureUsers
      && lib.elem "postgresql.target" base.systemd.services.nextcloud-setup.requires
      && lib.elem "postgresql.target" base.systemd.services.nextcloud-setup.after
    ))

    (expect "jellyfin: IMVDb is off by default and needs no key" (
      !(base.lanbat.secrets ? jellyfin-imvdb-env) && !lib.hasInfix "imvdb" (jellyfinBootstrap base)
    ))

    (expect "jellyfin: settings.imvdb requires the IMVDb key and hands it to the bootstrap" (
      jellyfinImvdb.lanbat.secrets ? jellyfin-imvdb-env
      && lib.hasInfix jellyfinImvdb.lanbat.secrets.jellyfin-imvdb-env.path (
        jellyfinBootstrap jellyfinImvdb
      )
      && failedAssertions jellyfinImvdb == [ ]
    ))

    (expect "audiobookshelf: the library defaults to drive b's audiobooks and Audible" (
      (absEnv base).LIBRARY_PATH == "/srv/storage/b/media/audiobooks"
      && (absEnv base).METADATA_PROVIDER == "audible"
      && base.lanbat.services.audiobookshelf.nfs.drives == [ "b" ]
      && (absEnv base).EXTERNAL_URL == "https://audiobooks.home.example.com"
    ))

    (expect "audiobookshelf: a profile moves the library and picks the provider" (
      (absEnv absMoved).LIBRARY_PATH == "/srv/storage/a/books/audio"
      && (absEnv absMoved).METADATA_PROVIDER == "audible.uk"
      && absMoved.systemd.services.audiobookshelf-match.environment.METADATA_PROVIDER == "audible.uk"
      && absMoved.lanbat.services.audiobookshelf.nfs.drives == [ "a" ]
      && lib.elem "srv-storage-a.mount" absMoved.systemd.services.audiobookshelf.bindsTo
      && failedAssertions absMoved == [ ]
    ))

    (expect "snapcast: its ports are no longer open to the whole LAN" (
      !lib.elem 1704 base.networking.firewall.allowedTCPPorts
      && !lib.elem 1705 base.networking.firewall.allowedTCPPorts
    ))

    (expect "snapcast: the Pi's snapclient and the Snapdroid box are admitted on both ports" (
      admits "192.0.2.11" base && admits "192.0.2.50" base
    ))

    (expect "snapcast: a listed client is admitted, and its rules are removed on stop" (
      admits "192.0.2.70" snapClients
      && lib.hasInfix "iptables -D INPUT -p tcp --dport 1705 -s 192.0.2.70 -j ACCEPT" (fwStop snapClients)
      && !lib.hasInfix "192.0.2.70" (fwStart base)
    ))

    (expect "snapcast: a client listed by MAC is admitted over IPv4 and IPv6, and removed on stop" (
      lib.all
        (
          cmd:
          lib.all
            (
              port:
              lib.hasInfix "${cmd} -I INPUT -p tcp --dport ${toString port} -m mac --mac-source 2c:d8:ae:00:00:01 -j ACCEPT" (
                fwStart snapMac
              )
              && lib.hasInfix "${cmd} -D INPUT -p tcp --dport ${toString port} -m mac --mac-source 2c:d8:ae:00:00:01 -j ACCEPT" (
                fwStop snapMac
              )
            )
            [
              1704
              1705
            ]
        )
        [
          "iptables"
          "ip6tables"
        ]
    ))

    (expect "snapcast: a client needs a MAC or an IPv4 address, and a well-formed MAC" (
      lib.any (lib.hasInfix "client tv") (failedAssertions snapEmpty)
      && lib.any (lib.hasInfix "2c-d8-ae") (failedAssertions snapBadMac)
    ))

    (expect "snapcast: a client must be an IPv4 address" (
      lib.any (lib.hasInfix "phone.lan") (failedAssertions snapBadHost)
    ))

    (expect "jackett: workload-native service stays private behind admin forward auth" (
      jackett.subdomain == "jackett"
      && jackett.port == 9117
      && jackett.auth == "forward-auth"
      && jackett.access.groups == [ "authentik Admins" ]
      && jackett.tier == "workload"
      && jackett.state == [ "jackett" ]
      &&
        jackett.units == [
          "jackett"
          "jackett-definitions"
          "jackett-qbittorrent-plugin"
        ]
      && jackettService.enable
      && !jackettService.openFirewall
      && lib.elem "workload-online.target" jackettUnit.wantedBy
    ))

    (expect "jackett: it listens only on loopback, and no firewall rule admits 9117" (
      lib.hasInfix " --ListenPrivate" jackettUnit.serviceConfig.ExecStart
      && !lib.hasInfix "--ListenPublic" jackettUnit.serviceConfig.ExecStart
      && lib.hasInfix "--Port 9117" jackettUnit.serviceConfig.ExecStart
      && !lib.elem 9117 base.networking.firewall.allowedTCPPorts
      && !lib.elem 9117 base.networking.firewall.allowedUDPPorts
    ))

    (expect "jackett: the plugin unit runs again before every qBittorrent start" (
      jackettPluginUnit.serviceConfig.Type == "oneshot"
      && !(jackettPluginUnit.serviceConfig.RemainAfterExit or false)
    ))

    (expect "jackett: its data directory is created for the jackett user on the workload layer" (
      lib.attrNames jackett.workloadDirs == [
        "jackett"
        "jackett/.config"
        "jackett/.config/Jackett"
      ]
      && lib.all (dir: dir.user == "jackett" && dir.group == "jackett" && dir.mode == "0700") (
        lib.attrValues jackett.workloadDirs
      )
    ))

    (expect "jackett: qBittorrent's plugin is configured from Jackett's generated API key" (
      lib.elem "workload-online.target" jackettPluginUnit.wantedBy
      && lib.elem "jackett.service" jackettPluginUnit.requires
      && lib.elem "jackett.service" jackettPluginUnit.after
      && lib.all (text: lib.hasInfix text jackettPluginUnit.script) [
        "/var/lib/jackett/.config/Jackett/ServerConfig.json"
        "/var/lib/qbittorrent/qBittorrent/nova3/engines"
        "http://127.0.0.1:9117"
        "YOUR_API_KEY_HERE"
        "chown qbt:qbt"
        "chmod 0600"
      ]
      && !lib.hasInfix "--ListenPublic" jackettUnit.serviceConfig.ExecStart
    ))

    # pkgs/jackett exists to be newer than nixpkgs'. If nixpkgs overtakes it,
    # the pin has become a downgrade: bump it (pkgs/jackett/update.sh) or drop it.
    (expect "jackett: the pinned build is not older than the locked nixpkgs'" (
      jackettService.package.pname == "jackett"
      && lib.versionAtLeast jackettService.package.version pkgs.jackett.version
    ))

    (expect
      "jackett: upstream's indexer definitions are synced on the workload layer, before Jackett starts"
      (
        jackettDefinitionsUnit.serviceConfig.Type == "oneshot"
        && lib.elem "jackett.service" jackettDefinitionsUnit.before
        && !lib.elem "jackett.service" (jackettDefinitionsUnit.requires or [ ])
        && lib.elem "workload-online.target" jackettDefinitionsUnit.wantedBy
        && lib.elem "workload-online.target" jackettDefinitionsTimer.wantedBy
        && lib.elem "workload-online.target" jackettDefinitionsTimer.partOf
        && jackettDefinitionsTimer.timerConfig.OnCalendar != null
        && jackettUnit.environment.XDG_CONFIG_HOME == "/var/lib/jackett/xdg"
        && lib.all (text: lib.hasInfix text jackettDefinitionsUnit.script) [
          "/var/lib/jackett/xdg/cardigann/definitions"
          "src/Jackett.Common/Definitions"
          "restart --no-block jackett.service"
        ]
      )
    )

    (expect "jackett: qBittorrent starts only after its plugin is configured" (
      lib.elem "jackett-qbittorrent-plugin.service" qbittorrentUnit.requires
      && lib.elem "jackett-qbittorrent-plugin.service" qbittorrentUnit.after
    ))

    (expect "snapcast: two clients can't share an address" (
      lib.any (lib.hasInfix "192.0.2.70") (failedAssertions snapDuplicate)
    ))

    (expect "music-assistant: a snapserver on another host is rejected" (
      lib.any (lib.hasInfix "#117") (failedAssertions maApart)
    ))

    (expect "the example profile's server has no failed assertion" (failedAssertions base == [ ]))
  ];

  partCases = map (c: c.value) (
    lib.filter (c: lib.mod c.index parts == part - 1) (
      lib.imap0 (index: value: { inherit index value; }) cases
    )
  );

  failures = lib.filter (x: x != null) partCases;
in
pkgs.runCommand "service-settings-check-${toString part}" { } ''
  if [ ${toString (lib.length failures)} -ne 0 ]; then
    echo "service settings checks (part ${toString part} of ${toString parts}) failed:" >&2
    ${lib.concatStringsSep "\n" (map (msg: "echo \"  - ${msg}\" >&2") failures)}
    exit 1
  fi
  touch $out
''
